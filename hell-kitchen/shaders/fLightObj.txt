#version 330 core

in vec3 vFragPos;
in vec3 vNormal;
in vec2 vTex;

out vec4 fColor;

struct Material{
	sampler2D diffuse;
	sampler2D specular;
	sampler2D emission;
	float shininess;
};

struct Light{
	vec3 position;

	vec3 ambient;
	vec3 diffuse;
	vec3 specular;
};

struct DirLight{
	vec3 direction;

	vec3 ambient;
	vec3 diffuse;
	vec3 specular;
};

struct PointLight{
	vec3 position;

	vec3 ambient;
	vec3 diffuse;
	vec3 specular;

	float constant;
	float linear;
	float quadratic;
};

struct FlashLight{
	vec3 direction;
	vec3 position;

	vec3 ambient;
	vec3 diffuse;
	vec3 specular;

	float constant;
	float linear;
	float quadratic;

	float inAngle;
	float outAngle;

	sampler2D cookie;

	bool isActive;
};

uniform Material material;
uniform DirLight dirLight;
uniform PointLight pointLight;
uniform FlashLight flashLight;
uniform vec2 viewPort;

#define POINTS 3
uniform PointLight pointLights[POINTS];

uniform vec3 fuViewPos;
uniform float fuTime;

// LIGHT FUNS
vec3 calcDirLight(DirLight, vec3, vec3);
vec3 calcPointLight(PointLight, vec3, vec3, vec3);
vec3 calcFlashLight(FlashLight, vec3, vec3, vec3);

void main(){
	vec3 result = vec3(0.0);
	vec3 color = vec3(0.0);
	vec3 norm = normalize(vNormal);
	vec3 viewDir = normalize(fuViewPos - vFragPos);

	//directional light
	//color = calcDirLight(dirLight, norm, viewDir);

	//1 point light
	//color += calcPointLight(pointLight, norm, vFragPos, viewDir);

	//few point lights
	for(int i = 0; i < POINTS; i++)
		color += calcPointLight(pointLights[i], norm, vFragPos, viewDir);
	
	//flashlight
	if (flashLight.isActive) color += calcFlashLight(flashLight, norm, vFragPos, viewDir);

	//emission
	vec3 emission = vec3(0.0);
	if(texture(material.specular, vTex).r == 0.0){
		//emission = texture(material.emission, vTex + vec2(0.0, fuTime)).rgb;
		emission = texture(material.emission, vTex).rgb;
		
		emission *= (sin(fuTime) * 0.4 + 0.6) * 2.0;
		emission.x = emission.y;
		emission.y -= emission.y;
		emission.z -= emission.z;
		emission *= 0.15;
	}

	//out
	result = color + emission;
	//result = color;
	fColor = vec4(result, 1.0);
}

vec3 calcDirLight(DirLight light, vec3 norm, vec3 viewDir){
	vec3 lightDir = normalize(-light.direction);

	vec3 ambient = light.ambient * vec3(texture(material.diffuse, vTex));

	float diff = max(dot(norm, lightDir), 0.0);
	vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, vTex));
	
	vec3 reflectDir = reflect(-lightDir, norm);
	float spec = pow(max(dot(reflectDir, viewDir), 0.0), material.shininess);
	vec3 specular = light.specular * spec * vec3(texture(material.specular, vTex));

	return ambient + diffuse + specular;
}

vec3 calcPointLight(PointLight light, vec3 norm, vec3 fragPos, vec3 viewDir){
	vec3 lightDir = normalize(light.position - fragPos);

	vec3 ambient = light.ambient * vec3(texture(material.diffuse, vTex));

	float diff = max(dot(norm, lightDir), 0.0);
	vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, vTex));
	
	vec3 reflectDir = reflect(-lightDir, norm);
	float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
	vec3 specular = light.specular * spec * vec3(texture(material.specular, vTex));

	float d = length(light.position - fragPos);
	float attenuation = 1.0 / (light.constant + light.linear * d + light.quadratic * (d * d));

	ambient *= attenuation;
	diffuse *= attenuation;
	specular *= attenuation;

	return (ambient + diffuse + specular);
}

vec3 calcFlashLight(FlashLight light, vec3 norm, vec3 fragPos, vec3 viewDir){
	vec3 lightDir = normalize(light.position - fragPos);

	float theta = dot(normalize(-light.direction), lightDir);
	float e = light.inAngle - light.outAngle;
	float intensity = clamp((theta - light.outAngle) / e, 0.0, 1.0);

	if(theta > light.outAngle){
		vec3 ambient = light.ambient * vec3(texture(material.diffuse, vTex));

		float diff = max(dot(norm, lightDir), 0.0);
		vec3 diffuse = light.diffuse * diff * vec3(texture(material.diffuse, vTex));
	
		vec3 reflectDir = reflect(-lightDir, norm);
		float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
		vec3 specular = light.specular * spec * vec3(texture(material.specular, vTex));

		float d = length(light.position - fragPos);
		float attenuation = 1.0 / (light.constant + light.linear * d + light.quadratic * (d * d));
		//analog
		//float attenuation = smoothstep(0.0, 1.0, (theta - light.outAngle) / e);

		diffuse *= intensity;
		specular *= intensity;

		diffuse *= attenuation;
		specular *= attenuation;
		
		//cookie
		vec2 winCoord = gl_FragCoord.xy / viewPort;
		float cookieAttenuation = length(vec3(texture(light.cookie, winCoord)));

		return (diffuse * cookieAttenuation + specular * cookieAttenuation);
	} else 
		return light.ambient * vec3(texture(material.diffuse, vTex));
}
